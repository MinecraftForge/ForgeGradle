plugins {
    id 'java-gradle-plugin'
    id 'eclipse'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'org.ajoberstar.grgit' version '4.1.0'
    id 'com.github.ben-manes.versions' version '0.39.0'
}

version = (grgit.describe(longDescr: true, tags: true) ?: 'unknown-unknown-unknown').split('-').with { "${it[0]}.${it[1]}" }
println('Version: ' + version + ' Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

sourceSets {
    common
    mcp
    patcher
    userdev
}

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

configurations {
    all {
        exclude group: 'org.ow2.asm'
    }
    mcpImplementation.extendsFrom commonImplementation
    patcherImplementation.extendsFrom commonImplementation
    userdevImplementation.extendsFrom mcpImplementation
    implementation.extendsFrom mcpImplementation, patcherImplementation, userdevImplementation
}

jar {
    from sourceSets.common.output
    from sourceSets.mcp.output
    from sourceSets.patcher.output
    from sourceSets.userdev.output
}

task sourcesJar(type: Jar) {
    from sourceSets.common.allJava
    from sourceSets.mcp.allJava
    from sourceSets.patcher.allJava
    from sourceSets.userdev.allJava
    archiveClassifier = 'sources'
}

repositories {
    mavenLocal()
    maven { url = 'https://maven.minecraftforge.net/' }
}

license {
    header = file('HEADER')
    ext {
        name = 'ForgeGradle'
        year = 2018
        fullname = 'Forge Development LLC'
    }
    exclude '**/*.properties'
    exclude 'net/minecraftforge/gradle/common/diff/'
    exclude 'net/minecraftforge/gradle/common/util/JavaVersionParser.java'
}

wrapper {
    gradleVersion = '7.1'
    distributionType = Wrapper.DistributionType.ALL
}

dependencies {
    commonImplementation gradleApi()
    commonImplementation 'commons-io:commons-io:2.8.0'
    commonImplementation 'com.google.code.gson:gson:2.8.6'
    commonImplementation 'com.google.guava:guava:30.1-jre'
    commonImplementation 'de.siegmar:fastcsv:2.0.0'
    commonImplementation 'net.minecraftforge:artifactural:3.0.8'
    commonImplementation 'org.apache.maven:maven-artifact:3.6.3'
    commonImplementation 'org.apache.httpcomponents:httpclient:4.5.13'
    commonImplementation 'net.minecraftforge:srgutils:0.4.3'
    commonImplementation 'net.minecraftforge:DiffPatch:2.0.7:all'

    mcpImplementation sourceSets.common.output
    patcherImplementation sourceSets.mcp.output
    patcherImplementation sourceSets.common.output
    userdevImplementation sourceSets.mcp.output
    userdevImplementation sourceSets.common.output
}

//Gradle doesn't add it's own source when doing the API. So lets hack it in!
import org.gradle.plugins.ide.eclipse.model.*
import org.gradle.plugins.ide.eclipse.model.internal.*
project.extensions.eclipse.classpath.file.whenMerged { Classpath cp ->
    def gradleSrc = gradle.gradleHomeDir.absolutePath.replace(File.separator, '/') + '/src/'
    cp.entries.each { entry ->
        if ((entry in AbstractLibrary) && entry.library.file.name.startsWith('gradle-')) {
            def type = (entry.library.file.name =~ "^gradle(-(.*))?-(${gradle.gradleVersion})")[0][2]
            if (type == 'api') type = 'core-api' //Gradle name is different for cores
            if (type == '') type = 'core'
            entry.sourcePath = new FileReferenceFactory().fromPath(gradleSrc + type)
        }
    }
}

gradlePlugin {
    plugins {
        mcp {
            id = 'net.minecraftforge.gradle.mcp'
            implementationClass = 'net.minecraftforge.gradle.mcp.MCPPlugin'
        }
        patcher {
            id = 'net.minecraftforge.gradle.patcher'
            implementationClass = 'net.minecraftforge.gradle.patcher.PatcherPlugin'
        }
        userdev {
            id = 'net.minecraftforge.gradle'
            implementationClass = 'net.minecraftforge.gradle.userdev.UserDevPlugin'
        }

        // legacy definitions
        forgedev_mcp {
            id = 'net.minecraftforge.gradle.forgedev.mcp'
            implementationClass = 'net.minecraftforge.gradle.mcp.MCPPlugin'
        }
        forgedev_patcher {
            id = 'net.minecraftforge.gradle.forgedev.patcher'
            implementationClass = 'net.minecraftforge.gradle.patcher.PatcherPlugin'
        }
        forgedev {
            id = 'net.minecraftforge.gradle.forgedev'
            implementationClass = 'net.minecraftforge.gradle.patcher.PatcherPlugin'
        }
    }
    automatedPublishing = false
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
    repositories {
        maven {
            if (System.env.MAVEN_USER) {
                url 'https://maven.minecraftforge.net/'
                authentication {
                    basic(BasicAuthentication)
                }
                credentials {
                    username = System.env.MAVEN_USER ?: 'not'
                    password = System.env.MAVEN_PASSWORD ?: 'set'
                }
            } else {
                url 'file://' + rootProject.file('repo').getAbsolutePath()
            }
        }
    }
}
