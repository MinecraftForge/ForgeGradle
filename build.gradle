apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'

group = 'net.minecraftforge.gradle'
version = '1.0-SNAPSHOT'
archivesBaseName = 'ForgeGradle'
targetCompatibility = '1.6'
sourceCompatibility = '1.6'

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes 'version':project.version, 'javaCompliance': project.targetCompatibility
        attributes 'group':project.group
    }
}

configurations {
    deployerJars
}

dependencies {
    compile gradleApi()

    compile 'net.sourceforge.argo:argo:3.4' // json stuff? perhaps replace with json slurper and stuff?
    compile 'net.sf.opencsv:opencsv:2.3' // reading CSVs.. also used by SpecialSource
    compile 'net.md-5:SpecialSource:1.5' // this is needed...
    compile 'com.cloudbees:diff4j:1.1' // for difing and patching
    compile 'com.github.abrarsyed.jastyle:jAstyle:1.2' // formatting
    compile 'org.ow2.asm:asm-debug-all:4.1' // Debug version, so we can have generics!

    compile 'com.github.jponge:lzma-java:1.3' // replaces the LZMA binary
    compile 'com.nothome:javaxdelta:2.0.1' // GDIFF implementation for BinPatches
    compile 'org.apache.shiro:shiro-all:1.2.2' // Ant style patern matching
    compile 'com.google.code.gson:gson:2.2.4' // Used instead of Argo for buuilding changelog.
    compile 'com.github.tony19:named-regexp:0.2.3' // 1.7 regexp behaviour.

    // just for the extension object
    //compile "edu.sc.seis.gradle:launch4j:latest.integration"

    // stupid maven
    deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
}

artifacts { archives jar }

uploadArchives {
    repositories {
        if (project.hasProperty("filesmaven")) {
            logger.info('Publishing to files server')

            mavenDeployer {
                configuration = configurations.deployerJars

                repository(url: project.filesmaven.url) {
                    authentication(userName: project.filesmaven.username, privateKey: project.filesmaven.key)
                }

                pom {
                    groupId = project.group
                    version = project.version
                    artifactId = project.archivesBaseName
                    project {
                        name project.archivesBaseName
                        packaging 'jar'
                        description 'Gradle plugin for Forge'
                        url 'https://github.com/MinecraftForge/ForgeGradle'

                        scm {
                            url 'https://github.com/MinecraftForge/ForgeGradle'
                            connection 'scm:git:git://github.com/MinecraftForge/ForgeGradle.git'
                            developerConnection 'scm:git:git@github.com:MinecraftForge/ForgeGradle.git'
                        }

                        issueManagement {
                            system 'github'
                            url 'https://github.com/MinecraftForge/ForgeGradle/issues'
                        }

                        licenses {
                            license {
                                name 'Eclipse Public License (EPL), Version 1.0'
                                url 'http://www.eclipse.org/legal/epl-v10.html'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id 'AbrarSyed'
                                name 'Abrar Syed'
                                roles { role 'developer' }
                            }
                        }
                        developers {
                            developer {
                                id 'LexManos'
                                name 'Lex Manos'
                                roles { role 'developer' }
                            }
                        }
                    }
                }
            }
        }
        else
        {
            add project.repositories.mavenLocal()
        }
    }
}
