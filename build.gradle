apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'

group = 'net.minecraftforge.gradle'

if (project.hasProperty("push_release")) {
	if (System.env.BUILD_NUMBER == null)
		throw new IllegalArgumentException('Can not push a release without specifying a build number in $BUILD_NUMBER')
	version = '1.2.0.' + System.env.BUILD_NUMBER
} else {
	version = '1.2-SNAPSHOT'
}
	
archivesBaseName = 'ForgeGradle'
targetCompatibility = '1.6'
sourceCompatibility = '1.6'

repositories {
    mavenLocal()
    maven {
        name = "forge"
        url = "http://files.minecraftforge.net/maven"
    }
    maven {
        // because Srg2Source needs an eclipse dependency.
        name = "eclipse"
        url = "https://repo.eclipse.org/content/groups/eclipse/"
    }
    maven {
        // because SpecialSource doesnt have a full release
        name = "sonatype"
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    mavenCentral()
    maven {
        name = "mojang"
        url = "https://libraries.minecraft.net/"
    }
}

jar {
    manifest {
        attributes 'version':project.version, 'javaCompliance': project.targetCompatibility
        attributes 'group':project.group
    }
}

configurations {
    deployerJars
    compileOnly
}

dependencies {
    compile gradleApi()
    
    // moved to the beginning to be the overrider
    compile 'org.ow2.asm:asm-debug-all:5.0.3'
    compile 'com.google.guava:guava:18.0'

    compile 'net.sf.opencsv:opencsv:2.3' // reading CSVs.. also used by SpecialSource
    compile 'com.cloudbees:diff4j:1.1' // for difing and patching
    compile 'com.github.abrarsyed.jastyle:jAstyle:1.2' // formatting
    compile 'net.sf.trove4j:trove4j:2.1.0' // because its awesome.
    
    compile 'com.github.jponge:lzma-java:1.3' // replaces the LZMA binary
    compile 'com.nothome:javaxdelta:2.0.1' // GDIFF implementation for BinPatches
    compile 'com.google.code.gson:gson:2.2.4' // Used instead of Argo for buuilding changelog.
    compile 'com.github.tony19:named-regexp:0.2.3' // 1.7 Named regexp features
    
    compile 'net.md-5:SpecialSource:1.7.3' // deobf and reobs
    
    // because curse
    compile 'org.apache.httpcomponents:httpclient:4.3.3'
    compile 'org.apache.httpcomponents:httpmime:4.3.3'
    
    // mcp stuff
    compile 'de.oceanlabs.mcp:RetroGuard:3.6.6'
    compile 'de.oceanlabs.mcp:mcinjector:3.2-SNAPSHOT'
    compile 'net.minecraftforge.srg2source:Srg2Source:3.2-SNAPSHOT'

    // stupid maven
    deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
    
    //Stuff used in the GradleStart classes
    compileOnly 'com.mojang:authlib:1.5.16'
    compileOnly "net.minecraft:launchwrapper:1.11"
}

compileJava {
	options.deprecation = true
	options.compilerArgs += ["-Werror"]
	//options.compilerArgs += ["-Werror", "-Xlint:unchecked"]
}

javadoc {
    classpath += configurations.compileOnly

    // linked javadoc urls.. why not...
    options.addStringOption 'link', 'https://gradle.org/docs/current/javadoc/'
    options.addStringOption 'link', 'http://docs.guava-libraries.googlecode.com/git-history/v18.0/javadoc'
    options.addStringOption 'link', 'http://asm.ow2.org/asm50/javadoc/user/'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc
    classifier = "javadoc"
}

artifacts {
    archives jar
    archives javadocJar
}

//These should add a compile time only dependancies to eclipse, ide, and javac, but NOT to the generated POM on maven.
sourceSets.main.compileClasspath += [ configurations.compileOnly ]
idea.module.scopes.PROVIDED.plus += [ configurations.compileOnly ]
eclipse.classpath.plusConfigurations += [ configurations.compileOnly ]

test {
    if (project.hasProperty("filesmaven")) // disable this test when on the forge jenkins
    {
        exclude "**/ExtensionMcpMappingTest*"
        exclude "**/ExtensionForgeVersionTest*"
    }
}

uploadArchives {
    repositories {
        if (project.hasProperty("filesmaven")) {
            logger.info('Publishing to files server')

            mavenDeployer {
                configuration = configurations.deployerJars

                repository(url: project.filesmaven.url) {
                    authentication(userName: project.filesmaven.username, privateKey: project.filesmaven.key)
                }

                pom {
                    groupId = project.group
                    version = project.version
                    artifactId = project.archivesBaseName
                    project {
                        name project.archivesBaseName
                        packaging 'jar'
                        description 'Gradle plugin for Forge'
                        url 'https://github.com/MinecraftForge/ForgeGradle'

                        scm {
                            url 'https://github.com/MinecraftForge/ForgeGradle'
                            connection 'scm:git:git://github.com/MinecraftForge/ForgeGradle.git'
                            developerConnection 'scm:git:git@github.com:MinecraftForge/ForgeGradle.git'
                        }

                        issueManagement {
                            system 'github'
                            url 'https://github.com/MinecraftForge/ForgeGradle/issues'
                        }

                        licenses {
                            license {
                                name 'Eclipse Public License (EPL), Version 1.0'
                                url 'http://www.eclipse.org/legal/epl-v10.html'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id 'AbrarSyed'
                                name 'Abrar Syed'
                                roles { role 'developer' }
                            }
                        }
                        developers {
                            developer {
                                id 'LexManos'
                                name 'Lex Manos'
                                roles { role 'developer' }
                            }
                        }
                    }
                }
            }
        }
        else
        {
            add project.repositories.mavenLocal()
            logger.info('Publishing to repo folder')
            
            mavenDeployer {
                repository(url: 'file://localhost/' + project.file('repo').getAbsolutePath())
            }
        }
    }
}

// write out version so its convenient for doc deployment
file('build').mkdirs()
file('build/version.txt').text = version;
